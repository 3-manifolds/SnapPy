; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{5C82D346-BE09-4D61-93D2-7A3C3F9A8DDC}
AppName=SnapPy
AppVerName=SnapPy
AppPublisher=Marc Culler and Nathan Dunfield
AppPublisherURL=http://snappy.computop.org
AppSupportURL=http://snappy.computop.org
AppUpdatesURL=http://snappy.compytop.org
DefaultDirName={pf}\SnapPy
DefaultGroupName=SnapPy
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename=InstallSnapPy-Dbg
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "dist\SnapPy\SnapPy.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "dist\SnapPy\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "vc_redist_installers\vc_redist_2015_x86.exe"; DestDir: {tmp}; Flags: deleteafterinstall

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\SnapPy"; Filename: "{app}\SnapPy.exe"; IconFileName: "{app}\snappy\SnapPy.ico"
Name: "{group}\{cm:UninstallProgram,SnapPy}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\SnapPy"; Filename: "{app}\SnapPy.exe"; Tasks: desktopicon; IconFileName: "{app}\snappy\SnapPy.ico"

[Run]
Filename: "{tmp}\vc_redist_2015_x86.exe"; Check: VCRedistNeedsInstall; StatusMsg: Installing Visual Studio 2015 C++ CRT Libraries..
Filename: "{app}\SnapPy.exe"; Description: "{cm:LaunchProgram,SnapPy}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{app}\snappy"

[Code]
const
  { Visual C++ 2015 UpgradeCodes, which should apply to any update of this package. }
  VC_2015_REDIST_X86 = '{65E5BD06-6392-3027-8C26-853107D3CF1A}';

function MsiEnumRelatedProducts(lpUpgradeCode: string; dwReserved: DWORD;
  iProductIndex: DWORD; lpProductBuf: string): UINT;
  external 'MsiEnumRelatedProductsW@msi.dll stdcall';

function VCRedistNeedsInstall: Boolean;
var
  I: Integer;
  ProductBuf: string;
begin
  I := 0;
  SetLength(ProductBuf, 39);
  while MsiEnumRelatedProducts(VC_2015_REDIST_X86, 0, I, ProductBuf) = 0 do
  begin
    I := I + 1;
    SetLength(ProductBuf, 39);
  end;
  Result := I = 0;
end;

 
